plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.0.0'
    id 'maven-publish'
}

java {
    toolchain.languageVersion.set(JavaLanguageVersion.of(17))
}

group = 'me.playbosswar.com'
version = '6.2.3'
description = 'CommandTimer'

repositories {
    mavenLocal()
    maven {
        url = 'https://repo1.maven.org/maven2'
    }
    maven {
        url = 'https://papermc.io/repo/repository/maven-public/'
    }
    maven {
        url = 'http://repo.extendedclip.com/content/repositories/placeholderapi/'
        allowInsecureProtocol = true
    }
    maven {
        url = 'https://repo.codemc.io/repository/maven-snapshots/'
    }
}

shadowJar {
    dependencies {
        include(dependency('net.wesjd:anvilgui:1.5.3-20220302.153253-5'))
        include(dependency('fr.minuskube.inv:smart-invs:1.2.7'))
        include(dependency('com.github.cryptomorin:XSeries:8.6.2'))
        include(dependency('joda-time:joda-time:2.10.13'))
        include(dependency('org.jeasy:easy-rules-core:4.1.0'))
        include(dependency('org.apache.commons:commons-text:1.9'))
    }
    archiveFileName = "commandtimer-java17.jar"
}

dependencies {
    implementation 'com.github.cryptomorin:XSeries:8.6.2'
    implementation 'fr.minuskube.inv:smart-invs:1.2.7'
    implementation 'net.wesjd:anvilgui:1.5.3-20220302.153253-5'
    implementation 'joda-time:joda-time:2.10.13'
    implementation 'org.jeasy:easy-rules-core:4.1.0'
    implementation 'com.googlecode.json-simple:json-simple:1.1.1'
    implementation 'org.apache.commons:commons-text:1.9'
    compileOnly 'io.papermc.paper:paper-api:1.18.2-R0.1-SNAPSHOT'
    compileOnly 'me.clip:placeholderapi:2.10.9'
    compileOnly 'org.jetbrains:annotations:23.0.0'
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'me.playbosswar.com'
            artifactId = 'commandtimer'
            version = '6.0'

            from components.java
        }
    }
}