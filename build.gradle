plugins {
    id 'java'
    id 'io.github.goooler.shadow' version '8.1.8'
    id 'maven-publish'
}

java {
    toolchain.languageVersion.set(JavaLanguageVersion.of(21))
}


group = 'me.playbosswar.com'
version = '8.7.6'
description = 'CommandTimer'

repositories {
    mavenLocal()
    maven {
        url = 'https://repo1.maven.org/maven2'
    }
    maven {
        url = 'https://papermc.io/repo/repository/maven-public/'
    }
    maven {
        url = 'http://repo.extendedclip.com/content/repositories/placeholderapi/'
        allowInsecureProtocol = true
    }
    maven {
        url = 'https://repo.codemc.io/repository/maven-snapshots/'
    }
}

shadowJar {
    dependencies {
        include(dependency('fr.minuskube.inv:smart-invs:1.2.7'))
        include(dependency('com.github.cryptomorin:XSeries:8.6.2'))
        include(dependency('joda-time:joda-time:2.10.13'))
        include(dependency('org.jeasy:easy-rules-core:4.1.0'))
        include(dependency('org.apache.commons:commons-text:1.10.0'))
        include(dependency('io.sentry:sentry:7.0.0'))
    }
    // Needed in case the server software already packs sentry
    relocate 'io.sentry', 'me.playbosswar.com.commandtimer.sentry'
    archiveFileName = "commandtimer-java21.jar"
}

dependencies {
    implementation 'com.github.cryptomorin:XSeries:8.6.2'
    implementation 'fr.minuskube.inv:smart-invs:1.2.7'
    implementation 'joda-time:joda-time:2.10.13'
    implementation 'org.jeasy:easy-rules-core:4.1.0'
    implementation 'com.googlecode.json-simple:json-simple:1.1.1'
    implementation 'org.apache.commons:commons-text:1.10.0'
    implementation 'io.sentry:sentry:7.0.0'
    compileOnly 'io.papermc.paper:paper-api:1.21-R0.1-SNAPSHOT'
    compileOnly 'me.clip:placeholderapi:2.10.9'
    compileOnly 'org.jetbrains:annotations:23.1.0'
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'me.playbosswar.com'
            artifactId = 'commandtimer-java21'
            version = '8.7.6'

            from components.java
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/titivermeesch/CommandTimer"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}